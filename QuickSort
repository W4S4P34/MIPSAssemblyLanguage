.data
	# ReadFile
						# Filename for input
	fin: 		.asciiz "D:/Project/MIPS-Assembly-Language/input_sort.txt"
	fout:		.asciiz "D:/Project/MIPS-Assembly-Language/output_sort.txt"							
	buffer: 	.space 1024		# Space to handle convertion
	# Escape Sequence
	CR:		.asciiz "\n"
	Spacebar:	.asciiz " "
.text
	.globl main

main:
#**********************#
	# Open the input file
	li	$v0, 13 			# System call for opening file
 	la	$a0, fin 			# Input filename
 	li	$a1, 0				# Flags to read
	li	$a2, 0				# Ignore mode
 	syscall 				# Open the file (File descriptor returned in $v0)

 	move 	$s0, $v0 			# Save file descriptor in $s0

	li	$t1, 0				# Initialize "count" variable
	li	$t2, 1				# Initialize "base" variable
	li	$t3, 0				# Initialize "sum" variable


#**********************#
	# Open the output file
	li	$v0, 13 			# System call for opening file
 	la	$a0, fout 			# Output filename
 	li	$a1, 1				# Flags to write
	li	$a2, 0				# Ignore mode
 	syscall 				# Open the file (File descriptor returned in $v0)

 	move 	$s1, $v0 			# Save file descriptor in $s0 


#**********************#
	# Read the input file
readNumber:
	# Read number of elements
 	li 	$v0, 14 			# System call for reading from file
 	move	$a0, $s0 			# Put the file descriptor in $a0
 	la 	$a1, buffer			# Address of buffer from which to read
 	li 	$a2, 1				# Number of character to read				
 	syscall 				# Read from file 

	lb	$t0, buffer
		
	beq	$t0, 13, readNumber		# Endline with CR LF (\n\r) / Skip '\n'
	beq	$t0, 10, handleLF_Number	# Check whether buffer contains '\n' or not

	addi	$t0, $t0, -48			# Get real digit

	addi	$sp, $sp, -1	
	sb	$t0, ($sp)
	
	addi	$t1, $t1, 1

	j	readNumber
	returnLF_Number:


#**********************#
	# Store number of elements to $s2
	lw	$s2, ($sp)			# Store number of elements to $s2

	addi	$sp, $sp, 4			# Move current $sp back to initial position


#**********************#
	li	$t5, 0				# Init "elementCount" variable/ "count" variable of $t1 contains "digitCount"

readElements:
	returnSP_Elements:				# Should file iterator meet the ' ' character, it may continue to read file
		li 	$v0, 14 			# System call for reading from file
 		move	$a0, $s0 			# Put the file descriptor in $a0
 		la 	$a1, buffer			# Address of buffer from which to read
 		li 	$a2, 1				
 		syscall 				# Read from file 

		lb	$t0, buffer
	
		beq	$v0, $0, handleEOF
		beq	$t0, 13, readElements		# Check whether buffer contains '\n' or ' ' (Escape Sequence)
		beq	$t0, 10, readElements		# Endline with CR LF (\n\r) / Skip '\n'	and '\r'
		beq	$t0, 32, handleSP_Elements

		addi	$t0, $t0, -48			# Get real digit

		addi	$sp, $sp, -1	
		sb	$t0, ($sp)
	
		addi	$t1, $t1, 1

		j	readElements
	returnEOF:


#**********************#
	# Reverse the array after reading from file
	li	$t0, 2
	div	$s2, $t0
	mflo	$t0

	move 	$a0, $t0			# $a0 holds number of half input elements

	li	$t0, 4				# Calculate offset
	mult	$s2, $t0
	mflo	$t0

	addi	$t0, $t0, -4

	add	$sp, $sp, $t0

	la	$t1, ($sp)			# $t1 points to last element

	li	$t3, -1				# Calculate offset
	mult	$t3, $t0
	mflo	$t0

	add	$sp, $sp, $t0

	la	$t2, ($sp)			# $t2 points to first element

	li	$t0, 0				# Initialize "index" variable

	jal	reverseArray


#**********************# 
	# QuickSort
	la	$a0, ($sp)			# Load array into $a0 	 
	li	$a1, 0				# $a1 = low = 0
	move	$a2, $s2			# Move number of elements into $a2
	addi	$a2, $a2, -1			# Set $a2 = high = number of elements - 1
	jal 	QuickSort


#**********************#
	# Convert each integer to string and output
	li	$t0, 0				# $t0 holds index of each element
	la	$t1, ($sp)			# $t1 points to the array

convertItoS:
	addi	$t0, $t0, 1

	la	$t3, buffer			# $t3 points to space to handle convertion

	li	$t4, 0				# $t4 holds number of digits

	beq	$t0, $s2, EOF

	li	$t2, 32				# ' '
	sb	$t2, ($t3)

	addi	$t4, $t4, 1

EOF:
	lw	$t2, ($t1)			# Get number to convert and print and store to $t2

LoopItoS:
	addi	$t3, $t3, -1			# Point to next digit

	li	$t5, 10
	div	$t2, $t5
	mflo	$t2				# $t2 holds new value after being divided (Quotient)
	mfhi	$t5				# $t5 holds Remainder
	
	add	$t5, $t5, '0'			# Change digit into ASCII code
	sb	$t5, ($t3)

	addi	$t4, $t4, 1

	bne	$t2, $0, LoopItoS

	addi	$t1, $t1, 4

	# Output to file
	li  	$v0, 15   			# System call for write to file
	move	$a0, $s1    			# File descriptor 
	move	$a1, $t3   			# Address of buffer from which to write
	li	$a2, 0
	add	$a2, $a2, $t4       		# Buffer length
	syscall           			# Write to file
	
	bne	$t0, $s2, convertItoS		# If ($t0 != $s2), back to convertion function


#**********************#
	# Free the stack frame
	li	$t0, 4

	mult	$t0, $s2
	mflo	$t0

	add	$sp, $sp, $t0			# Free stack frame


#**********************#
 	# Close the input file
	li 	$v0, 16 			# System call for closing file 
 	move 	$a0, $s0 			# Restore file descriptor
 	syscall 				# Close file


#**********************#
 	# Close the output file
	li 	$v0, 16 			# System call for closing file 
 	move 	$a0, $s1 			# Restore file descriptor
 	syscall 				# Close file

	# Exit program
 	li 	$v0, 10 			# End the program
 	syscall 


#*********************************************************#
handleLF_Number:
	lb	$t0, ($sp)			# Get current digit
	addi	$sp, $sp, 1			# Move up the stack pointer

	mult	$t0, $t2			# Multiple digit by base
	mflo	$t0

	add	$t3, $t3, $t0			# Sum up all digits to create the number

	addi	$t1, $t1, -1			# Decrease the "count" variable

	li	$t4, 10				# $t4 = 10

	mult	$t2, $t4			# Increase the position of digit
	mflo	$t2

	bne	$t1, $0, handleLF_Number

	addi	$sp, $sp, -4			# Set $sp to proper position
	sw	$t3, ($sp)

	# Restore all temporary registers
	li	$t1, 0				# Reset "count" to 0
	li	$t2, 1				# Reset "base" to 1
	li	$t3, 0				# Reset "sum" to 0
	
	j	returnLF_Number


#*********************************************************#
handleEOF:
	beq	$t5, $s2, returnEOF		# If "elementCount" == Number of elements,
						# return back to main

	lb	$t0, ($sp)			# Get current digit
	addi	$sp, $sp, 1			# Move up the stack pointer

	mult	$t0, $t2			# Multiple digit by base
	mflo	$t0

	add	$t3, $t3, $t0			# Sum up all digits to create the number

	addi	$t1, $t1, -1			# Decrease the "count" variable

	li	$t4, 10				# $t4 = 10

	mult	$t2, $t4			# Increase the position of digit
	mflo	$t2

	bne	$t1, $0, handleEOF

	addi	$sp, $sp, -4			# Set $sp to proper position
	sw	$t3, ($sp)

	# Restore all temporary registers
	li	$t1, 0				# Reset "count" to 0
	li	$t2, 1				# Reset "base" to 1
	li	$t3, 0				# Reset "sum" to 0
	
	j	returnEOF


#*********************************************************#
handleSP_Elements:
	lb	$t0, ($sp)			# Get current digit
	addi	$sp, $sp, 1			# Move up the stack pointer

	mult	$t0, $t2			# Multiple digit by base
	mflo	$t0

	add	$t3, $t3, $t0			# Sum up all digits to create the number

	addi	$t1, $t1, -1			# Decrease the "count" variable

	li	$t4, 10				# $t4 = 10

	mult	$t2, $t4			# Increase the position of digit
	mflo	$t2

	bne	$t1, $0, handleSP_Elements

	addi	$t5, $t5, 1			# Increase "elementCount"

	addi	$sp, $sp, -4			# Set $sp to proper position
	sw	$t3, ($sp)

	# Restore all temporary registers
	li	$t1, 0				# Reset "count" to 0
	li	$t2, 1				# Reset "base" to 1
	li	$t3, 0				# Reset "sum" to 0
	
	j	returnSP_Elements


#*********************************************************#
reverseArray:
	lw	$t3, ($t1)			# $t3 and $t4 holds pair of elements symmetric
	lw	$t4, ($t2) 

	sw	$t3, ($t2)			# Swap those elements with each other
	sw	$t4, ($t1)
	
	addi	$t0, $t0, 1			# Increase index
	addi	$t1, $t1, -4			# Move $t1 pointer to next element
	add	$t2, $t2, 4			# Move $t2 pointer to previous element

	bne	$t0, $a0, reverseArray

	# Reset temp variables' values
	li	$t0, 0
	li	$t1, 0
	li	$t2, 0

	jr	$ra


#*********************************************************#
	# QuickSort  
Swap:						# Swap(int array[], int swap1, int swap2)
	addi 	$sp, $sp, -12

	sw	$t1, 0($sp)			# Save Pivot in Partition
	sw 	$t2, 4($sp)			# Save i in Partition
	sw	$t3, 8($sp)			# Save j in Partition
	
	sll	$t0, $a1, 2			# $t0 = 4 * swap1
	add	$t0, $a0, $t0			# $t0 takes the index of a[swap1]
	lw	$t1, 0($t0)			# $t1 = a[swap1]

	sll	$t2, $a2, 2			# $t2 = 4 * swap2
	add	$t2, $a0, $t2			# $t2 takes the index of a[swap2]
	lw	$t3, 0($t2)			# $t3 = a[swap2]
	
	sw 	$t1, 0($t2)			# Swap a[swap1] and a[swap2]
	sw	$t3, 0($t0)

	lw 	$t1, 0($sp)			# Load Pivot into $t1 again
	lw	$t2, 4($sp)			# Load i into $t2 again
	lw	$t3, 8($sp)			# Load j into $t3 again

	addi 	$sp, $sp, 12			# Free stack

	jr 	$ra				

Partition:
	addi 	$sp, $sp, -12
	
	sw	$a1, 0($sp)			# Store low
	sw	$a2, 4($sp)			# Store High
	sw	$ra, 8($sp)			# Return address

	sll	$t0, $a2, 2			# $t0 = 4*high
	add	$t0, $a0, $t0			# $t0 takes the address of a[high]
	lw	$t1, 0($t0)			# $t1 = a[high] = pivot
	
	addi	$t2, $a1, -1			# $t2 = low - 1 (Now $t2 = i)

	move 	$t3, $a1			# $t3 = low 	(Now $t3 = j)

	addi	$t4, $a2, -1			# $t4 = high - 1
	ForLoop:
		bgt	$t3, $t4, EndLoop	# If low >= high - 1, go to EndLoop
		
		sll	$t0, $t3, 2		# $t0 = 4 * j
		add	$t0, $a0, $t0		# $t0 is address of array[j]
		lw	$t5, 0($t0)		# $t5 = array[j]

		bge	$t5, $t1, EndForIf 	# If array[j] < pivot

		addi	$t2, $t2, 1		# i++

		move	$a1, $t2		# Move i to a1 to swap
		move 	$a2, $t3		# Move j to a2 to swap
		jal 	Swap	
										
		EndForIf:
			addi 	$t3, $t3, 1	# j++
			j	ForLoop

	EndLoop:
		lw	$a1, 0($sp)		# Load low back to $a1
		lw	$a2, 4($sp)		# Load high back to $a2

		addi 	$a1, $t2, 1		# $a1 = i++ = low

		jal 	Swap

		add	$v0, $0, $a1		# $v0 = i 
		
		lw	$ra, 8($sp)		# Return address

		addi	$sp, $sp, 12		# Restore the stack

		jr	$ra

QuickSort:
	addi	$sp, $sp, -16

	sw	$a0, 0($sp)			# Array
	sw	$a1, 4($sp)			# Low
	sw	$a2, 8($sp)			# High
	sw	$ra, 12($sp)			# Return address
	
	slt	$t0, $a1, $a2			# If low is < high, set $t1 = 1 else $t1 = 0
	beq 	$t0, $0, EndIf			# If low >= high then jump to EndIf
	
	jal 	Partition
	move 	$s3, $v0			# Pivot
	
	lw	$a1, 4($sp)			# $a1 = low
	addi 	$a2, $s3, -1			# $a2 = pi - 1
	jal 	QuickSort
	
	addi	$a1, $s3, 1			# $a1 = pi + 1
	lw	$a2, 8($sp)			# $a2 = high
	jal 	QuickSort

EndIf: 	
	lw	$a0, 0($sp)			# Restore Array 
	lw	$a1, 4($sp)			# Restore Low
	lw	$a2, 8($sp)			# Restore High
	lw	$ra, 12($sp)			# Restore Return Address 

	addi	$sp, $sp, 16			# Restore Stack

	jr	$ra
